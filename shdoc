#!/usr/bin/awk -f

BEGIN {
    doc_constants = ""
    doc_params = ""
    doc_environment = ""
    internal_functions = ""
    is_show_internal = 0
    toc_internals = "## Internal Functions\n"
    toc = "## Functions\n"
    
    if (! style) {
        style = "github"
    }

    styles["github", "h1", "from"] = ".*"
    styles["github", "h1", "to"] = "# &"

    styles["github", "h2", "from"] = ".*"
    styles["github", "h2", "to"] = "## &"

    styles["github", "h3", "from"] = ".*"
    styles["github", "h3", "to"] = "### &"

    styles["github", "h4", "from"] = ".*"
    styles["github", "h4", "to"] = "#### &"

    styles["github", "h5", "from"] = ".*"
    styles["github", "h5", "to"] = "#### &"

    styles["github", "code", "from"] = ".*"
    styles["github", "code", "to"] = "```&"

    styles["github", "/code", "to"] = "```"

    styles["github", "const", "from"] = "^(\\S+)(\\s+|$)"
    styles["github", "const", "to"] = "**\\1**: "
    
    styles["github", "environment", "from"] = "^(\\S+)(\\s+|$)"
    styles["github", "environment", "to"] = "**\\1**: "
    
    styles["github", "param", "from"] = "^(\\S+)\\s+(\\S+)\\s*\\[([^\\]]*)]"
    styles["github", "param", "to"] = "**\\1** (\\2)[default: **\\3**]:"
    
    styles["github", "argN", "from"] = "^(\\$[0-9]) (\\S+)"
    styles["github", "argN", "to"] = "**\\1** (\\2):"

    styles["github", "arg@", "from"] = "^\\$@ (\\S+)"
    styles["github", "arg@", "to"] = "**...** (\\1):"

    styles["github", "li", "from"] = ".*"
    styles["github", "li", "to"] = "* &"

    styles["github", "i", "from"] = ".*"
    styles["github", "i", "to"] = "_&_"

    styles["github", "anchor", "from"] = ".*"
    styles["github", "anchor", "to"] = "[&](#&)"

    styles["github", "exitcode", "from"] = "([>!]?[0-9]{1,3}) (.*)"
    styles["github", "exitcode", "to"] = "**\\1**: \\2"
}

function render(type, text) {
    return gensub( \
        styles[style, type, "from"],
        styles[style, type, "to"],
        "g",
        text \
    )
}

function reset() {
    has_example = 0
    has_args = 0
    has_exitcode = 0
    has_stdout = 0
    has_stderr = 0
}

/^[[:space:]]*# @show-internal/ {
    is_show_internal = 1
}

/^[[:space:]]*# @internal/ {
    is_internal = 1
}

/^[[:space:]]*# @file/ {
    sub(/^[[:space:]]*# @file /, "")
    filedoc = render("h1", $0) "\n"
}

/^[[:space:]]*# @brief/ {
    sub(/^[[:space:]]*# @brief /, "")
    filedoc = filedoc "\n" $0
}

/^[[:space:]]*# @description/ {
    in_description = 1
    in_example = 0

    reset()

    docblock = ""
}

in_description {
    if (/^[^[[:space:]]*#]|^[[:space:]]*# @[^d]|^[[:space:]]*[^#]/) {
        if (!match(docblock, /\n$/)) {
            docblock = docblock "\n"
        }
        in_description = 0
    } else {
        sub(/^[[:space:]]*# @description /, "")
        sub(/^[[:space:]]*# /, "")
        sub(/^[[:space:]]*#$/, "")

        docblock = docblock "\n" $0
    }
}

in_example {
    if (! /^[[:space:]]*#[ ]{3}/) {
        in_example = 0

        docblock = docblock "\n" render("/code") "\n"
    } else {
        sub(/^[[:space:]]*#[ ]{3}/, "")

        docblock = docblock "\n" $0
    }
}

/^[[:space:]]*# @example/ {
    in_example = 1

    docblock = docblock "\n" render("h4", "Example")
    docblock = docblock "\n\n" render("code", "bash")
}

/^[[:space:]]*# @arg/ {
    if (!has_args) {
        has_args = 1

        docblock = docblock "\n" render("h4", "Arguments") "\n\n"
    }

    sub(/^[[:space:]]*# @arg /, "")

    $0 = render("argN", $0)
    $0 = render("arg@", $0)

    docblock = docblock render("li", $0) "\n"
}

/^[[:space:]]*# @environment-header/ {
    if (doc_environment == "") {
        doc_environment = render("h2", "Environments Variables") "\n\n"
    }
    sub(/^[[:space:]]*# @environment-header /, "")

    doc_environment = doc_environment render("h3", $0) "\n"
}

/^[[:space:]]*# @environment/ {
    if (doc_environment == "") {
        doc_environment = render("h2", "Environments Variables") "\n\n"
    }

    sub(/^[[:space:]]*# @environment /, "")

    $0 = render("environment", $0)

    doc_environment = doc_environment render("li", $0) "\n"
}

/^[[:space:]]*# @param-header/ {
    if (doc_params == "") {
        doc_params = render("h2", "Parameters") "\n\n"
    }
    sub(/^[[:space:]]*# @param-header /, "")

    doc_params = doc_params render("h3", $0) "\n"
}

/^[[:space:]]*# @param/ {
    if (doc_params == "") {
        doc_params = render("h2", "Parameters") "\n\n"
    }

    sub(/^[[:space:]]*# @param /, "")

    $0 = render("param", $0)

    doc_params = doc_params render("li", $0) "\n"
}

/^[[:space:]]*# @constant-header/ {
    if (doc_constants == "") {
        doc_constants = render("h2", "Constants") "\n\n"
    }
    sub(/^[[:space:]]*# @constant-header /, "")

    doc_constants = doc_constants render("h3", $0) "\n"
}

/^[[:space:]]*# @constant/ {
    if (doc_constants == "") {
        doc_constants = render("h2", "Constants") "\n\n"
    }

    sub(/^[[:space:]]*# @constant /, "")

    $0 = render("const", $0)

    doc_constants = doc_constants render("li", $0) "\n"
}

/^[[:space:]]*# @noargs/ {
    docblock = docblock "\n" render("i", "Function has no arguments.") "\n"
}

/^[[:space:]]*# @exitcode/ {
    if (!has_exitcode) {
        has_exitcode = 1

        docblock = docblock "\n" render("h4", "Exit codes") "\n\n"
    }

    sub(/^[[:space:]]*# @exitcode /, "")

    $0 = render("exitcode", $0)

    docblock = docblock render("li", $0) "\n"
}

/^[[:space:]]*# @see/ {
    sub(/[[:space:]]*# @see /, "")

    $0 = render("anchor", $0)
    $0 = render("li", $0)

    docblock = docblock "\n" render("h5", "See also") "\n\n" $0 "\n"
}

/^[[:space:]]*# @stdout/ {
    has_stdout = 1

    sub(/^[[:space:]]*# @stdout /, "")

    docblock = docblock "\n" render("h4", "Output on stdout")
    docblock = docblock "\n\n" render("li", $0) "\n"
}

/^[[:space:]]*# @stderr/ {
    has_stderr = 1

    sub(/^[[:space:]]*# @stderr /, "")

    docblock = docblock "\n" render("h4", "Output on stderr")
    docblock = docblock "\n\n" render("li", $0) "\n"
}

/^[ \t]*(function([ \t])+)?([a-zA-Z0-9_:-]+)([ \t]*)(\(([ \t]*)\))?[ \t]*\{/ && docblock != "" {
    func_name = gensub(\
        /^[ \t]*(function([ \t])+)?([a-zA-Z0-9_:-]+)[ \t]*\(.*/, \
        "\\3()", \
        "g" \
    )
    url = func_name
    if (style == "github") {
        # https://github.com/jch/html-pipeline/blob/master/lib/html/pipeline/toc_filter.rb#L44-L45
        url = tolower(url)
        gsub(/[^[:alnum:] -]/, "", url)
        gsub(/ /, "-", url)
    }
    if (is_internal) {
        is_internal = 0
        internal_functions = internal_functions render("h3", func_name) "\n" docblock "\n"
        toc_internals = toc_internals "* [" func_name "](#" url ")" "\n"
    } else {
        doc = doc render("h3", func_name) "\n" docblock "\n"
        toc = toc "* [" func_name "](#" url ")" "\n"
    }

    docblock = ""
    reset()
}

END {
    if (filedoc != "") {
        print filedoc "\n"
    }
    if (doc_constants != "") {
        print doc_constants "\n"
    }
    if (doc_params != "") {
        print doc_params "\n"
    }
    if (doc_environment != "") {
        print doc_environment "\n"
    }
    print toc
    print ""
    print doc
    if (internal_functions != "" && is_show_internal == 1) {
        print "\n" toc_internals "\n\n" internal_functions
    }
}
